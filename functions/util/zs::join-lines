#!/usr/bin/env zsh
# Copyright (c) 2023 Sebastian Gniazdowski
#
# zsqb – zs…quote…braces
#

util/zs::join-lines() {

# Set options
builtin emulate -L zsh -o extendedglob -o noshortloops \
                        -o warncreateglobal -o typesetsilent \
                        -o nopromptsubst

# Set $0 with a new trick - use of %x prompt expansion
0=${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}

local -A Opts
builtin zparseopts ${${(M)ZSH_VERSION:#(5.[8-9]|6.[0-9])}:+-F} \
    -D -E -A Opts -- d: ||return 7

local QSEP_P=$Opts[-d]
[[ $1 == -- ]]&&shift
reply=()

integer IDX=1 INC
local Q NL=$'\n\r\f\v' QSEP

for Q ("$@");do
    ((INC))&&IDX+=1
    [[ $Q == *([\\]|\|\||\&\&) ]]&&\
        {INC=0;QSEP=$QSEP_P;}||\
        {INC=1;QSEP=;}
    reply[IDX]+=${Q%%[\\$~NL]##}$QSEP
done

# Testable for content
(($#reply))

}
